#!ipxe

#设置 http 根目录
set http-url http://192.168.85.235/

# 设置背景图 ARM 目前好像不支持
# console --picture ${http-url}/tftp/ipxe/configs/bg.png --left 400 --right 840 --top 220 --bottom 220

# 设置默认超时时间
isset ${menu-timeout} || set menu-timeout 0
set submenu-timeout ${menu-timeout}

########################## MAIN MENU ##########################

# 主菜单开始标记
:start

# 显示主菜单标题，并显示 ip 地址
menu iPXE Boot Menu for ${ip} --${manufacturer}--${product}--${buildarch}--${platform}

#列项，可以指定热键，别名，添加 --gap --分割线
item --gap --               -------------------------- Operating systems --------------------------
item openwrt-x                  OpenWrt-X 1907
item --gap --               --------------------------- Advance Options ---------------------------
item bootlocal                  Boot from local drive
item config                     Configure settings
item shell				        iPXE shell
item reboot                     Reboot
item
item --key x exit               Exit iPXE and Continue BIOS Booting

########################## MAIN MENU ITEMS ##########################

# 在默认时间到时选择默认启动项
choose --timeout ${menu-timeout} --default ${menu-default} selected || goto cancel
goto ${selected}

# 取消
:cancel
echo You cancelled the menu, dropping you to a shell.

# 本地硬盘启动
:bootlocal
sanboot --no-describe --drive 0x80

# 配置
:config
config
goto start

# 打开命令行
:shell
echo Type 'exit' to get the back to the menu.
shell
set menu-timeout 0
set submenu-timeout 0
goto start

# 重启
:reboot
reboot

# 退出
:exit
exit

########################## openwrt-x MENU ITEMS ##########################

# openwrt-x 菜单开始标记
:openwrt-x

# 显示 openwrt-x 菜单标题
menu Boot OpenWrt-X Menu for for ${ip} --${manufacturer}--${product}--${buildarch}--${platform}

item --gap --               -------------------------- Arm Architecture --------------------------
item d2000_openwrt-x            Boot OpenWrt-X 1907 for D2000
item e2000_openwrt-x            Boot OpenWrt-X 1907 for E2000
item
item --key x back               Back to top menu ...

########################## openwrt-x MENU ITEMS ##########################

# 在默认时间到时选择默认启动项
choose --timeout ${submenu-timeout} --default ${submenu-timeout} selected || goto cancel
goto ${selected}

# 返回
:back
set submenu-timeout 0
clear submenu-default
goto start

#启动失败
:failed_boot
echo
echo Booting failed.
echo Press a key to return to the menu.
prompt
imgfree
goto start

# 启动 D2000 OpenWrt-X
:d2000_openwrt-x
echo Booting OpenWrt-X 1907 for D2000
kernel ${http-url}/platform/linux4197_d2000-glibc/vmlinux
initrd ${http-url}/platform/linux4197_d2000-glibc/cpio.gz
imgargs vmlinux console=ttyAMA0,115200 rw rootwait factory_env
boot || goto failed_boot

# 启动 E2000 OpenWrt-X
:e2000_openwrt-x
echo Booting OpenWrt-X 1907 for E2000
kernel ${http-url}/platform/linux419246_e2000-glibc/vmlinux
initrd ${http-url}/platform/linux419246_e2000-glibc/cpio.gz
imgargs vmlinux console=ttyAMA1,115200 rw rootwait factory_env
boot || goto failed_boot

########################## UNUSED MENU ITEMS ##########################

# 失败
:failed
echo Booting failed, dropping to shell.
goto shell

#下载失败
:failed_download
echo
echo Failed to download a file.
echo Press a key to return to the menu.
prompt
imgfree
goto start
